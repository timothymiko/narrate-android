apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.20.1'
    }
}

repositories {
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(file('../version.properties')))
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        applicationId 'com.datonicgroup.narrate.app'
        versionName versionProps['name']
        versionCode versionProps['code'].toInteger()
        multiDexEnabled true
    }

    dexOptions {
        incremental true
        javaMaxHeapSize "4g"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            // See for more help: https://github.com/romannurik/muzei
            def Properties localProps = new Properties()
            localProps.load(new FileInputStream(file('../local.properties')))
            def Properties keyProps = new Properties()
            assert localProps['keystore.props.file'];
            keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))

            storeFile file(keyProps["store"])
            keyAlias keyProps["alias"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
    }
    buildTypes {

        buildTypes.each {
            it.buildConfigField 'String', 'DROPBOX_API_KEY', dropbox_api_key
            it.buildConfigField 'String', 'DROPBOX_API_SECRET', dropbox_api_secret
//            it.buildConfigField 'String', 'PARSE_APP_ID', parse_app_id
//            it.buildConfigField 'String', 'PARSE_CLIENT_KEY', parse_client_key
        }

        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            minifyEnabled false
        }
    }
    productFlavors {
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }

    packagingOptions {
        exclude 'META-INF/maven/com.squareup.retrofit/retrofit/pom.properties'
        exclude 'META-INF/maven/com.squareup.retrofit/retrofit/pom.xml'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile files('libs/dropbox-android-sdk-1.6.1.jar')
    compile files('libs/httpmime-4.0.3.jar')
    compile files('libs/json_simple-1.1.jar')
    compile files('libs/FlurryAnalytics-5.1.0.jar')

    compile 'com.android.support:support-v4:23.2.1'
    compile ('com.android.support:appcompat-v7:23.1.0') {
        exclude module: 'support-v4'
    }
    compile 'com.android.support:design:23.2.1'
    compile 'com.android.support:cardview-v7:23.2.1'
    compile 'com.android.support:recyclerview-v7:23.2.1'
    compile 'com.googlecode.plist:dd-plist:1.16'

    compile 'com.squareup.okhttp:okhttp:2.6.0'
    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta3'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0-beta3'

    compile 'com.google.android.gms:play-services-plus:9.0.0'
    compile 'com.google.android.gms:play-services-maps:9.0.0'
    compile 'com.google.android.gms:play-services-location:9.0.0'

    compile 'joda-time:joda-time:2.9.1'
    compile project(':bypass')
    compile project(':materialdatetimepicker')
    compile 'com.wdullaer:materialdatetimepicker:2.1.0'
    compile project(':PhotoView')

    compile project(':ViewPagerIndicatorLibrary')
//    compile 'com.viewpagerindicator:library:2.4.1'

    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.google.apis:google-api-services-drive:v2-rev154-1.19.0' exclude module: 'httpclient'
    compile 'com.google.api-client:google-api-client-android:1.19.0' exclude module: 'httpclient'
    compile 'com.google.http-client:google-http-client-gson:1.19.0' exclude module: 'httpclient'
    compile('com.crashlytics.sdk.android:crashlytics:2.0.1@aar') {
        transitive = true;
    }

    compile 'com.google.maps.android:android-maps-utils:0.4'
    compile 'com.android.support:palette-v7:23.2.1'
    compile 'com.pnikosis:materialish-progress:1.7'
    compile 'com.github.bumptech.glide:glide:3.6.1'

    compile 'de.greenrobot:eventbus:2.4.0'

//    compile 'com.parse.bolts:bolts-android:1.+'
//    compile 'com.parse:parse-android:1.+'
    compile 'com.google.firebase:firebase-messaging:9.0.2'
}

android.buildTypes.all { buildType ->
    project.properties.any { property ->
        if (property.key.toLowerCase().startsWith("passcodelock.")) {
            buildType.buildConfigField "String", property.key.replace("passcodelock.", "").replace(".", "_").toUpperCase(), "\"${property.value}\""
        }
    }
}

apply plugin: 'com.google.gms.google-services'